#!/bin/sh 
# \
exec wish "$0" "$@"

#
# Or change to this: #!/usr/local/bin/wish8.0
#

# 2003.10.20
# Added save_as functionality
# Now accesses global variables as well
# Font selection made more general
# Better handling of temporary files


set myfont(cr)   {-*-screen-medium-*-*-*-12-*-*-*-*-*-*-*}
set myfont(bf)   {-*-application-bold-*-*-*-12-*-*-*-*-*-*-*}
#set myfont(cr)   {-adobe-courier-medium-r-normal--12-120-75-75-p-67-iso8859-1}
#set myfont(bf)   {-adobe-helvetica-bold-r-normal--12-120-75-75-p-70-iso8859-1}

set tmpfile [lindex $argv 0]
set command [lindex $argv 1]


# Procedure to save the displayed content:
# content: list, where every list element is going to be saved as one line
proc save_as {content} {

    #   Type names		Extension(s)
    #
    #---------------------------------------------------------
    set types {
	{"Text files"		{.txt .doc}	}
	{"All files"		*}
    }
    set file [tk_getSaveFile -filetypes $types -parent . \
		  -initialfile Untitled]

    if {$file != ""} {
	# Save contents to a file:
	set FILE [open $file w]
	foreach line $content {
	    puts $FILE $line
	}
	close $FILE;
	#puts "\nview.tk Window content saved as $file\n"
    }
}


# Build the interface
wm title . "$command"
tk_setPalette grey85

frame .buttons 
pack .buttons -side bottom -pady 1m

button .buttons.dismiss -font $myfont(bf) -relief groove -text "Close Window" -command "exit" -bd 1
pack .buttons.dismiss -fill x -side left -expand yes -padx 5
button .buttons.saveas -font $myfont(bf) -relief groove -text "Save As..." -command {save_as $content} -bd 1
pack .buttons.saveas -fill x -side left -expand yes -padx 5

text .text -font $myfont(cr) -relief sunken -bd 1 -bg grey80 -yscrollcommand ".scroll set" \
      -setgrid 1 -height 45
scrollbar .scroll -command ".text yview" -bd 1
pack .scroll -side right -fill y
pack .text -expand yes -fill both -padx 3

set TMP [open $tmpfile r]
while {[gets $TMP line] >= 0} {
    .text insert end $line\n
    lappend content $line
}

# done with the temporary file - close and delete
close $TMP
file delete $tmpfile
